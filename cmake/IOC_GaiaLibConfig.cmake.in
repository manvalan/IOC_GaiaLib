# IOC_GaiaLib CMake Config File
# This file is used by find_package(IOC_GaiaLib) in other CMake projects

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

# Find required dependencies
find_dependency(CURL REQUIRED)
find_dependency(LibXml2 REQUIRED)
find_dependency(Threads REQUIRED)
find_dependency(ZLIB REQUIRED)

# Find OpenMP (with macOS support)
if(APPLE)
    execute_process(
        COMMAND brew --prefix libomp
        OUTPUT_VARIABLE LIBOMP_PREFIX
        OUTPUT_STRIP_TRAILING_WHITESPACE
        ERROR_QUIET
    )
    if(LIBOMP_PREFIX)
        set(OpenMP_C_FLAGS "-Xpreprocessor -fopenmp -I${LIBOMP_PREFIX}/include")
        set(OpenMP_CXX_FLAGS "-Xpreprocessor -fopenmp -I${LIBOMP_PREFIX}/include")
        set(OpenMP_C_LIB_NAMES "omp")
        set(OpenMP_CXX_LIB_NAMES "omp")
        set(OpenMP_omp_LIBRARY "${LIBOMP_PREFIX}/lib/libomp.dylib")
    endif()
endif()
find_dependency(OpenMP REQUIRED)

# Include target definitions
include("${CMAKE_CURRENT_LIST_DIR}/IOC_GaiaLibTargets.cmake")

check_required_components(IOC_GaiaLib)
